#version 420 core

layout(location = 0) in vec2 aVertex;
layout(location = 1) in vec2 aUV;

layout(location = 2) in vec2 aPosition;
layout(location = 3) in vec2 aSize;
layout(location = 4) in float aRotation;
layout(location = 5) in vec2 aLbUv;
layout(location = 6) in vec2 aRtUv;
layout(location = 7) in vec4 aColor;
layout(location = 8) in float aLayer;

layout (std140, binding = 0) uniform GlobalMatrices
{
    mat4 projection;
    mat4 view;
};

out vec4 color;
out vec2 UV;

void main()
{
    color = aColor;
    UV = mix(aLbUv, aRtUv, aUV);

    vec2 local = aVertex * aSize;
    float s = sin(aRotation), c = cos(aRotation);
    vec2 rotated = vec2(local.x * c - local.y * s, local.x * s + local.y * c);
    vec3 world = vec3(aPosition + rotated, aLayer);

    gl_Position = projection * vec4(world, 1.0);
}